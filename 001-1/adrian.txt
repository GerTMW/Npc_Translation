// part of quest given by 021-2/kylian.txt
// Author: Jenalya

// state 1: player has the task to get the luggage from the harbor
// state 2: Adrian gave the luggage to the player
// state 3: player gave luggage to Kylian and Kylian asks who to talk to for the shop license

001-1,113,64,0|script|Adrian|213
{
    set @state, ((QUEST_NorthTulimshar & NIBBLE_5_MASK) >> NIBBLE_5_SHIFT);

    mes "[Adrian]";
    mes "\"Hallo. Bist du hier um das Gepäck einiges unserer Reisenden zu holen?\"";
    if (@state == 1)
        menu
            "Ja, Kylian schickt mich um sein Gepäck zu holen.",L_Get,
            "Nein.",L_No;
    menu
        "Nein.",L_No;
L_No:
    mes "[Adrian]";
    mes "\"Schade. Ich würde gern gehen um ein Bier zu trinken, aber ich muss warten bis das Gepäck abgeholt wird.\"";
    mes "Er seufzt.";
    goto L_Close;

L_Get:
    mes "[Adrian]";
    mes "\"Wundervoll! Zeig mir den Zettel.\"";
    mes "Er prüft das Papier, dass Kylian dir gab und wirft dir dann einen einen kritischen Blick zu.";
    next;
    getinventorylist;
    if ((checkweight("LeatherSuitcase", 1) == 0) || (@inventorylist_count == 100))
        goto L_Inventory;
    mes "[Adrian]";
    mes "\"In Ordnung, hier ist es.\"";
    mes "Adrain reicht dir einen sehr schweren Koffer.";
    getitem "LeatherSuitcase", 1;
    set @state, 2;
    set QUEST_NorthTulimshar, (QUEST_NorthTulimshar & ~(NIBBLE_5_MASK)) | (@state << NIBBLE_5_SHIFT);
    goto L_Close;

L_Inventory:
    mes "[Adrian]";
    mes "\"Der Koffer ist ziemlich schwer. Du siehst nicht aus, als ob du ihn tragen könntest.";
    mes "Vielleicht, wenn wenn du von den anderen Sachen ein paar loswirst.\"";
    goto L_Close;

L_Close:
    set @state, 0;
    set @inventorylist_count, 0;
    close;
}
