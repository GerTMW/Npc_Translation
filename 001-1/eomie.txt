// A professor at Tulimshar's magic academy, involved in quest:
// Anwar needs a magic fertilizer for his field
// Quest uses Nibble 3 of QUEST_NorthTulimshar
// Author: Jenalya
// state 1: Anwar sent to Tinris for help to get a magic fertilizer
// state 2: Tinris asked for chocolate cake as present for his girlfriend
// state 3: Tinris asked for ingredients for the fertilizer
// state 4: Tinris gave you the fertilizer
// state 5: Anwar tried it out and it doesn't work well, sent you back to Tinris
// state 6: Tinris asked for more ingredients for a new try
// state 7: Tinris says it's really hard, gives you the fertilizer and tells you to ask Eomie to cast a spell on it
// state 8: Eomie sends you to get some stuff so she can interrupt her spell on the spot of grass
// state 9: Eomie casted a spell on the fertilizer
// state 10: Anwar used it and it worked. he asks you to help him preparing a thank-you-gift for each Tinris and Eomis
// state 11: Anwar asked to bring present to Tinris
// state 12: gave present to Tinris
// state 13: Anwar asked to bring present to Eomie
// state 14: gave present to Eomie
// state 15: reported back to Anwar and done

001-1,71,23,0|script|Eomie|164
{
    set @state, ((QUEST_NorthTulimshar & NIBBLE_3_MASK) >> NIBBLE_3_SHIFT);

    set @grass_amount, 5;
    set @water_amount, 2;
    set @ice_amount, 2;

    set @silk_exp, 600;

    if (@state >= 14) goto L_Done;
    if (@state >= 9) goto L_CastedSpell;
    if (@state == 8) goto L_GetStuff;

    mes "[Eomie]";
    mes "\"Ich pflege dieses Fleckchen Gras in der Wüste.\"";
    if (@state != 7)
        goto L_Close;
    menu
        "Ich habe diesen magischen Dünger aus Tinris und muss einen Zauber über sie sprechen.",L_Next;

L_Next:
    mes "[Eomie]";
    mes "\"Oh? Ich verstehe, das ist eine schwierige Aufgabe.";
    mes "Ich würde mich freuen, dir zu helfen, aber leider kann ich die Magie nicht unterbrechen, die ich nutze, um diesen Flecken Gras am Leben zu halten.\"";
    next;
    mes "\"Es ist ungewöhnlich heiß im Moment, selbst für dieses Wüstengebiet. Das könnte etwas mit den Nachwirkungen des Erdbebens zu tun haben.\"";
    menu
        "Gibt es keine Möglichkeit?",L_Continue;

L_Continue:
    mes "Sie zögert.";
    set @state, 8;
    callsub S_Update_Mask;
    goto L_GetStuff;

L_GetStuff:
    // @state == 8
    mes "[Eomie]";
    mes "\"Ich könnte versuchen, meine Zauber zu unterbrechen, um den Zauber auf den Dünger zu legen, aber dann muss ich das Gras darauf vorbereiten.";
    mes "Bring mir " + @grass_amount + " Grassamen, " + @water_amount + " Wasserflaschen und " + @ice_amount + " Eiswürfel.\"";
    menu
        "Ich habe alles.",L_More,
        "Wo bekomme ich die Grassamen?",L_Hurnscald,
        "Wie bekomme ich Eiswürfel? Wir sind in der Wüste!",L_Mine,
        "Ok, ich werde alles besorgen.",L_Close;
L_More:
    if ((countitem("GrassSeed") < @grass_amount) || (countitem("BottleOfWater") < @water_amount) || (countitem("IceCube") < @ice_amount))
        goto L_NoItem;

    delitem "GrassSeed", @grass_amount;
    delitem "BottleOfWater", @water_amount;
    delitem "IceCube", @ice_amount;
    set @state, 9;
    callsub S_Update_Mask;
    mes "[Eomie]";
    mes "\"Sehr gut.\"";
    mes "Sie nimmt die Dinge, die du ihr gebracht hast und nutzt sie, um eine kühle Blase um den Grasfleck zu legen.";
    misceffect sfx_magic_nature;
    next;
    mes "[Eomie]";
    mes "\"Das sollte genügen, um den Grasfleck am Leben zu halten, bis ich den Zauber über den Dünger gesprochen habe. Und falls er schrumpft, habe die Samen, die du mir gebracht hast, um ihn wiederherzustellen.\"";
    next;
    mes "Sie nimmt den Dünger und spricht einige Beschwörungen. Nach einer Weile ist sie fertig und gibt ihn dir mit einem Lächeln zurück.";
    misceffect sfx_magic_nature;
    mes "[Eomie]";
    mes "\"Dies sollte dir weiterhelfen. Ich bin froh, dass du die Pflege der Pflanzen übernommen hast. Das tun heutzutag nicht mehr genügend Leute.\"";
    goto L_Close;

L_Hurnscald: // player asked how to get Grass Seeds
    mes "[Eomie]";
    mes "\"Eichhörnchen heben Grassamen auf, wenn sie Futter sammeln. Du findest sie run um Hurnscald. Um dorthin zu gelangen, musst du die Fähre im Hafenviertel nehmen.\"";
    goto L_Close;

L_Mine: // player asked how to get Ice Cubes
    mes "[Eomie]";
    mes "\"Hm, das ist in der Tat eine schwierige Aufgabe. Natürlich findest du sie auf dem Kontinent Kaizei, der hoch oben im Norden liegt und die meisten Zeit des Jahres mit Schnee bedeckt ist. Aber es dürfte schwierig sein, dorthin zu gelangen.\"";
    next;
    mes "\"Ich hörte, dass Eis Goblins in den Höhlen rund um Nivalis gesehen wurden. Vielleicht wirst du dort drin Eiswürfel finden.\"";
    goto L_Close;

L_CastedSpell: // @state >= 9 but below 14
    mes "[Eomie]";
    mes "\"Das Gras wurde durch die Unterbrechung nicht beschädigt. Ich hoffe, der magische Dünger ist nützlich für Anwar.\"";
    if (@state != 13)
        goto L_Close;
    menu
        "War er und er machte dieses Seiden Stirnband als Geschenk für dich.",L_Present,
        "(Murmle etwas und geh)",L_Close;

L_Present:
    if (countitem("SilkHeadband") < 1)
        goto L_NoItem;
    delitem "SilkHeadband", 1;
    getexp @silk_exp, 0;
    set @state, 14;
    callsub S_Update_Mask;
    mes "[Eomie]";
    mes "\"Oh, das ist sehr freundlich! Bitte sag ihm, ich bin sehr glücklich darüber.\"";
    mes "Sie lächelt.";
    goto L_Close;

L_Done: // @state >= 14
    mes "[Eomie]";
    mes "\"Hallo. Ich bin froh jemanden wie dich zu sehen, der sich für die Bedürfnisse der Pflanzen und Menschen engagiert.\"";
    goto L_Close;

L_NoItem:
    mes "[Eomie]";
    mes "\"Wo ist es?\"";
    goto L_Close;

L_Close:
    set @state, 0;
    set @grass_amount, 0;
    set @water_amount, 0;
    set @ice_amount, 0;
    close;

S_Update_Mask:
    set QUEST_NorthTulimshar, (QUEST_NorthTulimshar & ~(NIBBLE_3_MASK)) | (@state << NIBBLE_3_SHIFT);
    return;
}
