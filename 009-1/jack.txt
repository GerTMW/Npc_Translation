//#################################################################################
//#                                                                               #
//# This script file contains the npc scripts for two quests:                     #
//#                                                                               #
//# Quest1: Obtaining a Forest bow                                                #
//# Needed: About 20 Raw logs, 5000gp, some chatting and running                  #
//# Reward: Forest bow                                                            #
//#                                                                               #
//# Quest2: Obtaining a Wooden shield                                             #
//# Needed: 40 Raw logs, 5000gp                                                   #
//# Reward: Wooden shield and 2500exp                                             #
//#                                                                               #
//# Variables used:  @QUEST_Forestbow_state, nibbles 0 and 1                      #
//#################################################################################

// Lumberjack
009-1,40,45,0|script|Jack|141
{
    callfunc "ClearVariables";

    set @RAWLOGS_AMOUNT, 40;
    set @SHIELD_COST, 5000;
    set @QUEST_SHIELD_EXP, 2500;

    set @Q_Forestbow_MASK, NIBBLE_0_MASK;
    set @Q_Forestbow_SHIFT, NIBBLE_0_SHIFT;

    set @Q_Woodenshield_MASK, NIBBLE_1_MASK;
    set @Q_Woodenshield_SHIFT, NIBBLE_1_SHIFT;

    set @Q_Forestbow, ((QUEST_Forestbow_state & @Q_Forestbow_MASK) >> @Q_Forestbow_SHIFT);
    set @Q_Woodenshield, ((QUEST_Forestbow_state & @Q_Woodenshield_MASK) >> @Q_Woodenshield_SHIFT);

    set @inspector, ((QUEST_Hurnscald & NIBBLE_3_MASK) >> NIBBLE_3_SHIFT);

    if (@Q_Woodenshield >= 3) goto L_Shield_state_3;
    if (@Q_Woodenshield == 2) goto L_Shield_state_2;
    if (@Q_Woodenshield == 1) goto L_Shield_state_1;
    if (@Q_Forestbow > 1) goto L_Bow_state_2;

    mes "[Jack Lumber]";
    mes "\Hallo, mein Name ist Jack Lumber, der Feind aller Bäume.  Wenn du etwas Feuerholz brauchst, lass es mich wissen.\"";
    if (@inspector == 1)
        goto L_NohMask_Ask;
    goto L_NotInspector;

L_NotInspector:
    next;
    if (@Q_Forestbow < 1)
        goto L_Close;
    mes "Ich habe gehört, dass du kein lebendes Holz mehr auslieferst. Warum denn nicht?";
    next;
    goto L_Next;

L_Next:
    mes "[Jack Lumber]";
    mes "\"Warum nicht? Ich schätze mein Leben, deshalb!\"";
    next;
    menu
        "Was meinst du damit?", L_Next1;

L_Next1:
    mes "[Jack Lumber]";
    mes "\"Vor einer Woche wollte ich einen dieser Laubzweigbäume fällen im südwestlichen Wald. Diese Laubzweige sind die Bäume, welche mir das lebende Holz gegeben haben, von dem du gesprochen hast. Ich war gerade mit dem Hacken fertig, entfernte meine Axt und was passierte? Einer ihrer Zweige traf mich! Ich dachte mir, er fiel von selbst herunter oder der Wind war es. Es schmerzte! Ich schüttelte sie von mir und schlug wieder mit der Axt zu und noch ein Zweig traf mich! Ich wurde wütend und beschloss, alle Zweige abzuhacken in der Umbebung, sodass es nicht mehr vorkommen konnte. Aber ich konnte nicht glauben, was dann geschah.\"";
    next;
    menu
        "Was ist passiert? ", L_Next2;

L_Next2:
    mes "[Jack Lumber]";
    mes "\"Du hälst mich für wahnsinnig, wenn ich dir das erzähle.\"";
    next;
    menu
        "Werde ich nicht. Versprochen.", L_Next3;

L_Next3:
    mes "[Jack Lumber]";
    mes "\"OK. Nachdem ich ein paar Zweige abgehackt hatte, begann sich der ganze Baum zu bewegen. Seine Wurzeln rissen sich aus der Erde, all diese Zweige schlugen wie Wellen umher und ein Gesicht erschien auf dem Stamm. Der ganze Baum begann zu leben! Es war verrückt!\"";
    next;
    menu
        "Bist du weggerannt?", L_Next4,
        "Hast du gekämpft?", L_Next4;

L_Next4:
    mes "[Jack Lumber]";
    mes "\"Ich kämpfte, natürlich! Ich griff meine Axt und attakierte das Biest! Es traf mich hier *zeigt auf eine Prellung an seiner Schulter*, hier *hebt sein Hosenbein um die andere Prellung zu zeigen* und hier *hebt sein Hemd und zeigt einige noch schlimmere Prellungen*. Aber ich gab nicht auf. Ich hackte sie alle weg, Zweig für Zweig und am Ende traf ich seine Wurzeln, und er fiel zu Boden, regungslos.\"";
    next;
    menu
        "Du hast also gewonnen? Warum bist du dann so ängstlich?", L_Next5;

L_Next5:
    mes "[Jack Lumber]";
    mes "\"Naja, ich war erschöpft und ruhte mich aus. Einige Minuten vergingen und plötzlich wurde ich von einem Dutzend oder mehr dieser lebenden Bäume umgeben!\"";
    next;
    menu
        "Hast du die auch bekämpft? ", L_Next6;

L_Next6:
    mes "[Jack Lumber]";
    mes "\"Bist du verrückt? Ich zerstörte kaum eines dieser Biester. Ich war nicht in der Form weiter zu kämpfen. Ich rannte so schnell ich konnte. Glück für mich, dass die Monster sind nicht so schnell auf ihren kleinen Holzbeinen sind. Ha!\"";
    next;
    menu
        "Du fällst also keine Bäume mehr?", L_Next7;

L_Next7:
    mes "[Jack Lumber]";
    mes "\"Doch, das mache ich. Es ist mein Job. Aber ich schneide nicht länger diese Laubzweige, von denen ich erzähle. Ich weiß, der Bogenbauer will dieses Holz von ihnen, aber das ist nicht meine Sorge. Ich will nicht mein Leben riskieren für ein paar Goldstücke. Wenn du das willst, nur nach Südwesten gehen. Aber ich will niemals wieder dort hin.\"";
    next;
    menu
        "Südwestlich sagtest du? OK, Danke dir.", L_Next8;

L_Next8:
    set @Q_Forestbow, 2;
    callsub S_Update_Mask;
    goto L_Close;

L_Bow_state_2:
    // Bow state 4: The player has found a perfect piece of wood.
    if (@Q_Forestbow > 3) goto L_Bow_state_4;

    mes "\"Viel Glück beim Jagen dieser Monster - du wirst es brauchen.\"";
    if (@inspector != 1)
        goto L_Close;
    goto L_NohMask_Ask;

L_Bow_state_4:
    mes "[Jack Lumber]";
    mes "\"Du hast endlich das perfekte Stück lebendes Holz gefunden, das Alan braucht, um den Waldbogen herzustellen, nicht wahr?\"";
    next;
    menu
        "Ich konnte den Bogen noch nicht erschwingen...", L_Shield_state_0,
        "Nein ich schau noch danach.", L_Bow_state_4_reaffirm,
        "Ja, ich habe den Waldbogen jetzt.", L_Next9,
        "Ich bin ein Nahkampfkrieger, ich brauche keinen Bogen.", L_Shield_state_0;

L_Next9:
    // Check if the player tells the truth (continue on in any case)
    if (@Q_Forestbow > 4) goto L_Shield_state_0;

    mes "[Jack Lumber]";
    mes "\"Hrmph. Ich bin einer, der die Wahrheit zu sagen pflegt.\"";
    next;
    goto L_Shield_state_0;

L_Bow_state_4_reaffirm:
    mes "[Jack Lumber]";
    mes "\"Du solltest noch einmal mit Alan sprechen.\"";
    goto L_Close;

L_Shield_state_0:
    set @Q_Woodenshield, 1;
    callsub S_Update_Mask;

    if (Sex == 0)
        set @child_of_jack$, "meine eigene Tochter";
    if (Sex == 1)
        set @child_of_jack$, "mein eigener Sohn";

    mes "[Jack Lumber]";
    mes "\"Du scheinst dich nicht daran zu erinnern, wie ich mit diesen Biestern zutun hatte, kämpfend um mein Leben. Sie machten mir Angst, diese siebenzweigigen kleinen Schlümpfe! Noch nie, seit dem Tag, bin ich in die Nähe von ihnen gewesen. Und da kommst du und fällst sie alle einen nach dem anderen. Ich bin stolz auf dich, dass du es wagst, gegen diese Stämme zu kämpfen. Du bist stark als währst du  " + @child_of_jack$ + "!\"";
    next;
    goto L_Shield_state_1;

L_Shield_state_1:
    mes "[Jack Lumber]";
    mes "\"Ich habe eine Idee. Was würdest du über ein neues Schild sagen?\"";
    next;
    if (@inspector == 1)
        menu
            "Nein, Danke.", L_Close,
            "Ja bitte!", L_Yes,
            "Eigentlich möchte ich wissen, ob du seltsame Geschehnisse in der Stadt gesehen hast.", L_NohMask_Answer;
    menu
        "Nein, Danke.", L_Close,
        "Ja bitte!", L_Yes;

L_Yes:
    mes "[Jack Lumber]";
    mes "\"Alles was ich brauche ist " + @RAWLOGS_AMOUNT + " [Holzstück].  Es muss nicht die höhste Qualität des Holzes sein, einfach jedes Holz sollte funktionieren. Oh, und ich brauche auch " + @SHIELD_COST + " € für anderes Material.\"";
    next;
    set @Q_Woodenshield, 2;
    callsub S_Update_Mask;
    goto L_Close;

L_Shield_state_2:
    mes "[Jack Lumber]";
    mes "\"Lass' mich sehen, ob du genug von allem hast.\"";
    menu
        "Hier sind die sachen...", L_Next10,
        "Ich komme später noch einmal vorbei...", L_Close;

L_Next10:
    if (countitem("RawLog") < @RAWLOGS_AMOUNT)
        goto L_Not_enough_logs;
    if (Zeny < @SHIELD_COST)
        goto L_not_enough_money;
    getinventorylist;
    if (@inventorylist_count == 100)
        goto L_TooMany;
    delitem "RawLog", @RAWLOGS_AMOUNT;
    set Zeny, Zeny - @SHIELD_COST;
    getexp @QUEST_SHIELD_EXP, 0;
    getitem "WoodenShield", 1;
    set @Q_Woodenshield, 3;
    callsub S_Update_Mask;
    mes "[Jack Lumber]";
    mes "\"Setz dich.\"";
    mes "Jack sägt die Hölzer in Stücke, dann schleift er sie, bis sie sich glatt anfühlen.";
    mes "Die Anwendung einer stark riechenden Flüssigkeit färbt sie schließlich dunkel.";
    next;
    mes "[Jack Lumber]";
    mes "Er greift sich eins der zwei übrig bebliebenen Stücke und beginnt es in eine runde Form zu schnitzen, dann wiederholt er dies mit dem zweitem Stück - Schildgriffe, wie sie für dich aussehen.";
    next;
    mes "[Jack Lumber]";
    mes "Inzwischen hat die Sonne die anderen Stücke getrocknet. Jack legt sie nebeneinander, fügt einen Rahmen und Nägel zusammen.";
    mes "Das daraus resultierende Schild sieht bereits fertig aus, aber Jack gibt eine andere Flüssigkeit zu und ülässt es für ein paar Augenblicke trocknen.";
    next;
    mes "[Jack Lumber]";
    mes "Schließlich gibt er dir das Schild.";
    mes "\"Genieß' dein neues Schild!\"";
    mes "[" + @QUEST_SHIELD_EXP + " experience points]";
    next;
    goto L_Close;

L_Not_enough_logs:
    mes "[Jack Lumber]";
    mes "\"Du hast nicht genug Holz für das Schild. Für das Handwerk brauche ich insgesammt  " + @RAWLOGS_AMOUNT + " [Holzstück].\"";
    goto L_Close;

L_not_enough_money:
    mes "[Jack Lumber]";
    mes "\"Du hast nicht genug Geld. Ich brauche " + @SHIELD_COST + "€, um deinen Schild fertigzustellen.\"";
    goto L_Close;

L_TooMany:
    mes "[Jack Lumber]";
    mes "\"Du hast nicht genug Platz für das Schild. Komm wieder, wenn du es auch mitnehmen kannst.\"";
    goto L_Close;

L_Shield_state_3:
    mes "[Jack Lumber]";
    mes "\"Ich hoffe, dass mein Schild dir gute Dienste leisten wird!\"";
    next;
    if (@inspector != 1)
        goto L_Made_Shield;
    menu
        "Ich auch.", L_Made_Shield,
        "Hast du etwas gesehen, dass mit den jüngsten Diebstählen in der Stadt in Verbindung stehen könnte?", L_NohMask_Answer;

L_NohMask_Ask:
    menu
        "Ich behalte es im Auge.", L_Close,
        "Ich habe gehört, dass du kein lebendes Holz mehr auslieferst. Warum denn nicht?", L_Next,
        "Hast du etwas gesehen, dass mit den jüngsten Diebstöhle in der Stadt in Verbindung stehen könnte?", L_NohMask_Answer;

L_NohMask_Answer:
    mes "[Jack Lumber]";
    mes "\"Tut mir leid, nein.\"";
    goto L_Close;

L_Made_Shield:
    next;
    mes "[Jack Lumber]";
    mes "\"Apropos Schild ...\"";
    mes "\"Es hat mich in den ganzen Nivalis bestellungen völlig zurück geworfen, gerade jetzt wo dass Schiff bald ausläuft.\"";
    next;
    mes "\"Würdest du mir mit den bestellungen helfen? Ich werde dich auch für deine Arbeit entlohnen.\"";
    goto L_Daily;

L_Daily:
    set @dq_level, 30;
    set @dq_cost, 25;
    set @dq_count, 30;
    set @dq_name$, "RawLog";
    set @dq_friendly_name$, "raw logs";
    set @dq_money, 4000;
    set @dq_exp, 4000;

    callfunc "DailyQuest";

    next;

    mes "[Jack Lumber]";
    mes "\"Komm morgen wieder, ich lasse dich wissen falls ich Hilfe mit dem Versand benötige.\"";

    goto L_Close;

L_Close:
    set @inspector, 0;
    close;

S_Update_Mask:
    set QUEST_Forestbow_state, (QUEST_Forestbow_state & ~(@Q_Forestbow_MASK | @Q_Woodenshield_MASK)) | (@Q_Forestbow << @Q_Forestbow_SHIFT) | (@Q_Woodenshield << @Q_Woodenshield_SHIFT);
    return;
}
